system, "mkdir output";

/**********************************************************************************
*
* MAD-X input script for the MTE splitting configuration at 800 ms cycle time
* on the SFTPRO cycle.
* 30/03/2020 - Alexander Huschauer
************************************************************************************/
 
/******************************************************************
 * Energy and particle type definition
 ******************************************************************/

call, file="ps_hs_sftpro.beam";
BRHO      := BEAM->PC * 3.3356;

/******************************************************************
 * Call lattice files
 ******************************************************************/

call, file="ps.seq";
call, file="ps_hs_sftpro.str";
call, file="macros.madx";

/**********************************************************************************
 *                                  Tune matching
***********************************************************************************/

Qx := 0.255278;
! vertical tune according to 2015 chromaticity measurement
Qy := 0.29826;

use, sequence=PS;
match, use_macro;
        vary, name = kf;
        vary, name = kd;
        use_macro, name = ptc_twiss_macro(2,0,0);
        constraint, expr = table(ptc_twiss_summary,Q1)  = Qx;
        constraint, expr = table(ptc_twiss_summary,Q2)  = Qy;
jacobian,calls=50000,bisec=3, tolerance = 1E-15;
ENDMATCH;

exec, write_str_file("./output/ps_hs_sftpro.str");

/******************************************************************
 * Define fourfold PS sequence for Twiss in the islands
 ******************************************************************/

use, sequence = PS;
twiss;
PSL = table(summ, length);

PS4: SEQUENCE, L = 4*PSL, refer = entry;
PS, at = 0.0;
PS, at = PSL;
PS, at = 2*PSL;
PS, at = 3*PSL;
ENDSEQUENCE;

/**********************************************************************************
 * PTC Twiss for the core
***********************************************************************************/

! obtain smooth optics functions by slicing the elements
use, sequence=PS;
exec, ptc_twiss_macro(2,0,1);
exec, write_ptc_twiss("./output/ps_hs_sftpro.tfs");

/**********************************************************************************
 * PTC Twiss for the islands
***********************************************************************************/

x = 0.032;
px = 0.0;

! obtain smooth optics functions by slicing the elements
use, sequence=PS4;
exec, ptc_twiss_MTE_macro(2,x,px,1);
exec, write_ptc_twiss("./output/ps_hs_sftpro_islands.tfs");
